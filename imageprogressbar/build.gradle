apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'


def projectVersionCodeNr = Integer.parseInt(projectVersionCode);
def libGit = libGit;
def libUrl = libUrl;
def libDescription = libDescription
def libGroupId = libGroupId;
def libArtifactId = libArtifactId;


android {
    compileSdkVersion 27



    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 27
        versionName project.projectVersionName
        versionCode Integer.parseInt(project.projectVersionCode)
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}


group = libGroupId
version = projectVersionName


install {
    repositories.mavenInstaller {
        pom.project {
            name libArtifactId
            description libDescription
            url libUrl
            inceptionYear '2018'

            packaging 'aar'
            groupId libGroupId
            artifactId libArtifactId
            version projectVersionName

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url libLicenseUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = bintray_user
    key = bintray_apikey
    pkg {
        repo = 'android'
        name = libArtifactId
//        userOrg = bintray_user_org
        licenses = ['Apache-2.0']
        vcsUrl = libGit
        version {
            name = projectVersionName
            gpg {
                sign = true
//                passphrase = bintray_gpg_phrase
            }
        }
    }
    configurations = ['archives']
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.1.1'
}
